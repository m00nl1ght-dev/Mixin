
buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1'
    }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.github.hierynomus.license'

defaultTasks 'licenseFormat', 'check', 'build'

group = 'org.spongepowered'
archivesBaseName = 'mixin'
version = buildVersion + (buildType == 'RELEASE' ? '' : "-$buildType")

java {
    modularity.inferModulePath = true
    toolchain.languageVersion = JavaLanguageVersion.of(11)
}

repositories {
    mavenCentral()
    maven { url "https://m00nl1ght.dev/maven/" }
}

sourceSets {
    main {}
    universalTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
    universalTestExample {
        compileClasspath += universalTest.output
        compileClasspath += main.output
    }
}

dependencies {
    // api 'dev.m00nl1ght.clockwork.utils:config:0.7'
    api 'dev.m00nl1ght.clockwork.utils:logger:0.7'
    api 'dev.m00nl1ght.clockwork.utils:profiler:0.7'

    implementation 'com.google.guava:guava:30.1.1-jre'

    implementation "org.ow2.asm:asm-tree:$asmVersion"
    implementation "org.ow2.asm:asm-commons:$asmVersion"
    implementation "org.ow2.asm:asm-util:$asmVersion"

    universalTestImplementation configurations.implementation
}

processResources {
    // Include in final JAR
    from 'LICENSE.txt'
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }
    include '**/*.java'
    header file("HEADER.txt")
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-proc:none']
    options.deprecation = true
    options.encoding = 'utf8'
}
